# 用 sin 和 cos 旋转几何体

问题是所有点都获得相同的旋转值。我们需要做的是获取每个点的偏移量，但作为旋转偏移量。回想起高中数学，可以使用 tan()，如果给定 x 和 y，将返回一个角度。考虑一下，每个点都是与其他点的时间偏移量，所以将其添加到@Time 看看会发生什么：

```cpp
float angle = atan(@P.x, @P.y);

@P.x += sin(@Time+angle);
@P.y += cos(@Time+angle);
```

它在旋转，但在旋转时会做一些奇怪的事情。我们可能想要设置位置而不是添加到现有位置：

```cpp
float angle = atan(@P.x, @P.y);

@P.x = sin(@Time+angle);
@P.y = cos(@Time+angle);
```

它很接近，但如果与原始盒子进行比较，缩放比例会略有不同。我们还需要获取半径（即点到原点的距离），并将其乘以旋转以获得正确缩放的值：

```cpp
float angle = atan(@P.x, @P.y);
float r = length(@P);

@P.x = sin(@Time+angle)*r;
@P.y = cos(@Time+angle)*r;
```

在猪猪身上试试。发生了一些奇怪的事情，他的鼻子膨胀了，很奇怪。问题是半径；我们正在测量 3d 空间中的距离，但我们只需要到旋转轴（即 z 轴）的直接半径。如果我们只使用@Px 和 Py 测量长度，应该这么修复：

```cpp
float angle = atan(@P.x, @P.y);
float r = length(set(@P.x, @P.y));

@P.x = sin(@Time+angle)*r;
@P.y = cos(@Time+angle)*r;
```

完毕！这是将常规坐标转换为极坐标（角度和 r），然后再返回。单看极坐标本身就很有趣：

```cpp
float angle = atan(@P.x, @P.y);
float r = length(set(@P.x, @P.y));

@P.x = angle;
@P.y = r;
@P.z = 0;
```

如果看一下线框，你会发现它看起来像一个圆柱形 uv 投影，因为它就是这样。奇怪的拉伸是因为在做 uv 时会选择一个接缝点来分割边缘，我们在这里没有费心去做。

回到原来的旋转，如果把时间属性拿出来，换成通道，就得到了一个简单的旋转工具：

```cpp
float angle = atan(@P.x, @P.y);
float r = length(set(@P.x, @P.y));
float amount = ch('amount');

@P.x = sin(amount+angle)*r;
@P.y = cos(amount+angle)*r;
```

点击按钮获取滑块，滑开。会注意到“1”的数量旋转了大约 30 度，这是没有意义的。确实如此；sin/cos/atan 都以弧度表示。如果我们假设滑块是度数，您也需要将其转换为弧度：

```cpp
float angle = atan(@P.x, @P.y);
float r = length(set(@P.x, @P.y));
float amount = radians(ch('amount'));

@P.x = sin(amount+angle)*r;
@P.y = cos(amount+angle)*r;
```
